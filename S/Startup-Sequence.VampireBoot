; Startup Sequence for Vampire Boot
; ---------------------------------
;
; Date: 2017-06-22
; Author: Henrik Noerfjand Stengaard


; Add assigns required for mount and comfigure vampire boot
makedir RAM:VampireBoot
Assign >NIL: ENV: RAM:VampireBoot
copy ENVARC:~(#?.info) ENV: ALL QUIET
Assign >NIL: T: RAM:VampireBoot


; Configure vampire boot, if V key is pressed (rawkey code 52)
KeyPressed 52
IF WARN
  execute S:Configure-VampireBoot
ENDIF


; Configure vampire boot, if boot device doesn't exist
IF NOT EXISTS "SYS:Prefs/Env-Archive/BootDevice"
  execute S:Configure-VampireBoot
ENDIF


; TODO: Mount devices in mountlist
mount dh0:
mount dh1:


; Get boot device
set bootdevice "`type SYS:Prefs/Env-Archive/bootdevice`"


; Configure vampire boot, if boot device is empty
IF "$bootdevice" EQ ""
  execute S:Configure-VampireBoot
ENDIF


Assign SYS: $bootdevice
Assign C: SYS:C
Path C:

IF EXISTS SYS:S
  Assign S: SYS:S
ENDIF

IF EXISTS SYS:L
  Assign L: SYS:L
ENDIF

IF EXISTS SYS:Libs
  Assign LIBS: SYS:Libs
ENDIF

IF EXISTS SYS:Devs
  Assign DEVS: SYS:Devs
ENDIF

IF EXISTS SYS:Fonts
  Assign Fonts: SYS:Fonts
ENDIF

IF EXISTS SYS:Prefs/Env-Archive
  Assign ENVARC: SYS:Prefs/Env-Archive
ENDIF

CD SYS:

IF EXISTS SYS:S/Startup-Sequence
  Execute >NIL: SYS:S/Startup-Sequence
ENDIF

EndCLI >NIL: