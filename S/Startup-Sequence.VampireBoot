; Startup Sequence for Vampire Boot Installation
; ----------------------------------------------
;
; Date: 2017-06-21
; Author: Henrik Noerfjand Stengaard


; Clear screen
echo "*ec" 
echo "Vampire Boot"


; Set default stack and failat
stack 16384
failat 21


; Add assigns
C:MakeDir >NIL: RAM:T RAM:ENV RAM:ENV/Sys
Assign >NIL: ENV: RAM:Env
C:Copy ENVARC:~(#?.info) ENV: ALL QUIET
Assign >NIL: T: RAM:T


; Delete auto boot device, if V key is pressed (rawkey code 52)
KeyPressed 52
IF WARN
  IF EXISTS "SYS:Prefs/Env-Archive/AutoBootDevice"
    Delete >NIL: "SYS:Prefs/Env-Archive/AutoBootDevice"
    echo ""
    echo "Disabled auto boot device!"
  ENDIF
ENDIF


; Show continue dialog, if Vampire card is not detected
VampireTool CHECK
IF NOT $RC EQ 0
  set continue `RequestChoice "Continue" "Vampire card is not detected. Do you want to continue?" "Yes|No"`
  IF "$continue" EQ "0"
    QUIT
  ENDIF
ENDIF


; Build mount list, if it doesn't exist
IF NOT EXISTS DEVS:Mountlist
  echo ""
  echo "Building mountlist..."
  execute S:BuildMountlist
  echo "Done"
ENDIF


; Run configure vampire boot, if auto boot device doesn't exist
IF NOT EXISTS "SYS:Prefs/Env-Archive/AutoBootDevice"
  execute S:Configure-VampireBoot
ENDIF


; Reboot, if boot device doesn't exist
IF NOT EXISTS "SYS:Prefs/Env-Archive/BootDevice"
  REQUESTCHOICE "Error" "Error: Boot device doesn't exist!" "OK" >NIL: 
  reboot
ENDIF


; Reboot, if boot device is not defined
set bootdevice `type "SYS:Prefs/Env-Archive/BootDevice"`
IF "$bootdevice" EQ ""
  REQUESTCHOICE "Error" "Error: Boot device is not defined!" "OK" >NIL: 
  reboot
ENDIF


echo ""
echo "Booting device $bootdevice"
echo ""


; Mount devices in mountlist
C:MakeDir >NIL: T:Mount
Copy >NIL: DEVS:Mountlist T:Mount
Mount >NIL: T:Mount/~(#?.info) 


; Remove assigns
C:Delete >NIL: RAM:T ALL
C:Delete >NIL: RAM:ENV ALL
Assign >NIL: ENV: RAM:Env REMOVE
Assign >NIL: T: RAM:T REMOVE


; Boot device
execute S:BootDevice $bootdevice