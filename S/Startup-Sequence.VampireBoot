; Startup Sequence for Vampire Boot Installation
; ----------------------------------------------
;
; Date: 2017-06-19
; Author: Henrik Noerfjand Stengaard


; Clear screen
echo "*ec" 


; Add assigns
C:MakeDir >NIL: RAM:T RAM:ENV RAM:ENV/Sys
Assign >NIL: ENV: RAM:Env
C:Copy ENVARC:~(#?.info) ENV: ALL QUIET
Assign >NIL: T: RAM:T


; Build mount list, if it doesn't exist
IF NOT EXISTS DEVS:Mountlist
  execute S:BuildMountlist
ENDIF


; Execute configure vampire boot, if auto boot device doesn't exist
IF NOT EXISTS "SYS:Prefs/Env-Archive/AutoBootDevice"
  execute S:Configure-VampireBoot
ENDIF


; Reboot, if boot device doesn't exist
IF NOT EXISTS "SYS:Prefs/Env-Archive/BootDevice"
  REQUESTCHOICE "Error" "Error: Boot device doesn't exist!" "OK" >NIL: 
  reboot
ENDIF


; Reboot, if boot device is not defined
set bootdevice `type "SYS:Prefs/Env-Archive/BootDevice"`
IF "$bootdevice" EQ ""
  REQUESTCHOICE "Error" "Error: Boot device is not defined!" "OK" >NIL: 
  reboot
ENDIF


; Mount devices in mountlist
C:MakeDir >NIL: T:SDMount
Copy >NIL: DEVS:Mountlist T:SDMount
Mount >NIL: T:SDMount/~(#?.info) 


; Remove assigns
C:Delete >NIL: RAM:T ALL
C:Delete >NIL: RAM:ENV ALL
Assign >NIL: ENV: RAM:Env REMOVE
Assign >NIL: T: RAM:T REMOVE


; Boot device
execute S:BootDevice $bootdevice