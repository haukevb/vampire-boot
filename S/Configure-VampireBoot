; Configure Vampire Boot
; ----------------------
;
; Date: 2017-06-22
; Author: Henrik Noerfjand Stengaard
;
; A script for configure vampire boot, which presents a menu to configure boot device for Vampire SD card


; Build mountlist, if mountlist doesn't exist
IF NOT EXISTS "DEVS:Mountlist"
  SKIP buildmountlist
ENDIF


; Main Menu
; ---------
LAB mainmenu

echo "Boot device: " NOLINE >T:mainmenu
IF EXISTS "SYS:Prefs/Env-Archive/BootDevice"
  set bootdevice `type "SYS:Prefs/Env-Archive/BootDevice"`
  echo "$bootdevice" >>T:mainmenu
ELSE
  echo "N/A" >>T:mainmenu
ENDIF
echo "Rebuild device list" >>T:mainmenu
echo "CLI" >>T:mainmenu
echo "Exit" >>T:mainmenu

; Show main menu
C:ReqList CLONERT BUTTONS I T:mainmenu O ENV:mainmenu H "Configure Vampire Boot"
delete >NIL: T:mainmenu

; Boot device
IF "$mainmenu" eq "1" 
  SKIP bootdevice
ENDIF 

; Build mountlist
IF "$mainmenu" eq "2" 
  SKIP buildmountlist
ENDIF

; CLI
IF "$mainmenu" eq "3" 
  QUIT
ENDIF

SKIP reboot


; Boot device
; -----------

LAB bootdevice

IF NOT EXISTS "DEVS:Mountlist"
  REQUESTCHOICE "Error" "Mountlist 'DEVS:Mountlist' doesn't exist!*N*NTry build mountlist" "OK" >NIL: 
  SKIP BACK mainmenu
ENDIF

; Build select boot device menu from device names in mount list
set bootdevicemenu `grep ":$" <DEVS:Mountlist`
echo "$bootdevicemenu" >T:bootdevicemenu
rep T:bootdevicemenu " " "*N"

; Show select boot device menu
C:ReqList CLONERT BUTTONS I T:bootdevicemenu O ENV:bootdevicemenu H "Select Boot Device"
set lineregexp `sed "s/\(.\)*$/\1q;d/" ENV:bootdevicemenu`
sed "$lineregexp" "T:bootdevicemenu" >"SYS:Prefs/Env-Archive/BootDevice"
delete >NIL: T:bootdevicemenu

SKIP BACK mainmenu


; Build mountlist
; ---------------

LAB buildmountlist
echo "Building mountlist..."
`execute S:BuildMountlist`
echo "Done"
echo "Building mount startup..."
execute S:BuildMountStartup
echo "Done"
wait 2
SKIP BACK mainmenu


; Reboot
; ------
LAB reboot

echo ""
echo "Rebooting in 10 seconds..."

wait 10
reboot


; End
; ---
LAB end