; Configure Vampire Boot
; ----------------------
;
; Date: 2017-06-29
; Author: Henrik Noerfjand Stengaard
;
; A script for configure vampire boot, which presents a menu to configure boot device for Vampire SD card


; Main Menu
; ---------
LAB mainmenu

echo "Build mountlist" >T:_mainmenu
echo "Boot device: " NOLINE >>T:_mainmenu
IF EXISTS "SYS:Prefs/Env-Archive/BootDevice"
  set bootdevice `type "SYS:Prefs/Env-Archive/BootDevice"`
  echo "$bootdevice" >>T:_mainmenu
ELSE
  echo "N/A" >>T:_mainmenu
ENDIF
echo "CLI" >>T:_mainmenu
echo "Exit" >>T:_mainmenu

; Show main menu
set mainmenu "`C:ReqList CLONERT BUTTONS I=T:_mainmenu H="Configure Vampire Boot"`"
delete >NIL: T:_mainmenu

; Build mountlist
IF "$mainmenu" eq "1" 
  SKIP buildmountlist
ENDIF

; Boot device
IF "$mainmenu" eq "2" 
  SKIP bootdevice
ENDIF 

; CLI
IF "$mainmenu" eq "3" 
  QUIT
ENDIF

SKIP reboot


; Build mountlist
; ---------------

LAB buildmountlist
echo ""
echo "Building mountlist..."
set temp "`execute S:BuildMountlist`"
echo "Done"
echo ""
echo "Building mount startup..."
set temp "`execute S:BuildMountStartup DEVS:Mountlist S:Mount-Startup`"
echo "Done"
echo ""
echo "Mountlist and mount startup has been build."
echo ""
ask "Press ENTER to continue"

; Clear screen
echo "*ec" 

SKIP BACK mainmenu


; Boot device
; -----------

LAB bootdevice

IF NOT EXISTS "DEVS:Mountlist"
  REQUESTCHOICE "Error" "Mountlist 'DEVS:Mountlist' doesn't exist!*N*NTry build mountlist" "OK" >NIL: 
  SKIP BACK mainmenu
ENDIF

; Build select boot device menu from device names in mount list
set bootdevicemenu `grep ":$" <DEVS:Mountlist`
echo "Cancel" >T:_bootdevicemenu
echo "$bootdevicemenu" >>T:_bootdevicemenu
rep T:_bootdevicemenu " " "*N"

; Show boot device menu
set bootdevice "`C:ReqList CLONERT BUTTONS I=T:_bootdevicemenu H="Boot Device"`"
IF "$bootdevice" EQ "1"
  SKIP BACK mainmenu
ENDIF

; Set boot device
echo "$bootdevice" NOLINE >T:_bootdevice
echo "q;d" NOLINE >>T:_bootdevice
set lineregexp "`type T:_bootdevice`"
sed "$lineregexp" "T:_bootdevicemenu" >"SYS:Prefs/Env-Archive/BootDevice"
delete >NIL: T:_bootdevicemenu T:_bootdevice

SKIP BACK mainmenu


; Reboot
; ------
LAB reboot

echo ""
echo "Rebooting in 10 seconds..."

wait 10
reboot


; End
; ---
LAB end