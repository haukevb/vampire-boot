; Configure Vampire Boot
; ----------------------
;
; Date: 2017-06-21
; Author: Henrik Noerfjand Stengaard
;
; A script for configure vampire boot, which presents a menu to configure boot device for Vampire SD card


; Main Menu
; ---------
LAB mainmenu

echo "Select boot device" >T:mainmenu
echo "Rebuild device list" >>T:mainmenu
echo "Auto boot device: " NOLINE >>T:mainmenu
IF EXISTS "SYS:Prefs/Env-Archive/AutoBootDevice"
  echo "YES" >>T:mainmenu
ELSE
  echo "NO" >>T:mainmenu
ENDIF
echo "Boot device: " NOLINE >>T:mainmenu
IF EXISTS "SYS:Prefs/Env-Archive/BootDevice"
  set bootdevice `type "SYS:Prefs/Env-Archive/BootDevice"`
  echo "$bootdevice" >>T:mainmenu
ELSE
  echo "N/A" >>T:mainmenu
ENDIF
echo "CLI" >>T:mainmenu
echo "Reboot" >>T:mainmenu

; Show main menu
C:ReqList CLONERT BUTTONS I T:mainmenu O ENV:mainmenu H "Configure Vampire Boot"
delete >NIL: T:mainmenu

; Select Boot device
IF "$mainmenu" eq "1" 
  SKIP selectbootdevice
ENDIF 

; Rebuild device list
IF "$mainmenu" eq "2" 
  SKIP rebuilddevicelist
ENDIF

; Auto boot device
IF "$mainmenu" eq "3" 
  SKIP autobootdevice
ENDIF

; Boot device
IF "$mainmenu" eq "4" 
  SKIP end
ENDIF 

; CLI
IF "$mainmenu" eq "5" 
  QUIT
ENDIF

SKIP reboot


; Select boot device
; ------------------

LAB selectbootdevice

IF NOT EXISTS "DEVS:Mountlist"
  REQUESTCHOICE "Error" "Mountlist 'DEVS:Mountlist' doesn't exist!*N*NTry rebuild device list" "OK" >NIL: 
  SKIP BACK mainmenu
ENDIF

; Build select boot device menu from device names in mount list
set bootdevicemenu `grep ":$" <DEVS:Mountlist`
echo "$bootdevicemenu" >T:bootdevicemenu

; Show select boot device menu
C:ReqList CLONERT BUTTONS I T:bootdevicemenu O ENV:bootdevicemenu H "Select Boot Device"
set lineregexp `sed "s/\(.\)*$/\1q;d/" ENV:bootdevicemenu`
sed "$lineregexp" "T:bootdevicemenu" >"SYS:Prefs/Env-Archive/BootDevice"
delete >NIL: T:bootdevicemenu

SKIP BACK mainmenu


; Rebuild device list
; -------------------

LAB rebuilddevicelist
`execute S:BuildMountlist`
SKIP BACK mainmenu


; Auto Boot Device
; ----------------

LAB autobootdevice

IF EXISTS "SYS:Prefs/Env-Archive/AutoBootDevice"
  delete >NIL: "SYS:Prefs/Env-Archive/AutoBootDevice"
Else
  echo "" NOLINE >"SYS:Prefs/Env-Archive/AutoBootDevice"
EndIF 

SKIP BACK mainmenu


; Reboot
; ------
LAB reboot

echo "Rebooting in 10 seconds"

wait 10
reboot


; End
; ---
LAB end