; Configure Vampire Boot
; ----------------------
;
; Date: 2017-06-23
; Author: Henrik Noerfjand Stengaard
;
; A script for configure vampire boot, which presents a menu to configure boot device for Vampire SD card


; Build mountlist, if mountlist doesn't exist
IF NOT EXISTS "DEVS:Mountlist"
  SKIP buildmountlist
ENDIF


; Main Menu
; ---------
LAB mainmenu

echo "Boot device: " NOLINE >T:mainmenu
IF EXISTS "SYS:Prefs/Env-Archive/BootDevice"
  set bootdevice `type "SYS:Prefs/Env-Archive/BootDevice"`
  echo "$bootdevice" >>T:mainmenu
ELSE
  echo "N/A" >>T:mainmenu
ENDIF
echo "Build mountlist" >>T:mainmenu
echo "CLI" >>T:mainmenu
echo "Exit" >>T:mainmenu

; Show main menu
set mainmenu "`C:ReqList CLONERT BUTTONS I=T:mainmenu H="Configure Vampire Boot"`"
delete >NIL: T:mainmenu

; Boot device
IF "$mainmenu" eq "1" 
  SKIP bootdevice
ENDIF 

; Build mountlist
IF "$mainmenu" eq "2" 
  SKIP buildmountlist
ENDIF

; CLI
IF "$mainmenu" eq "3" 
  QUIT
ENDIF

SKIP reboot


; Boot device
; -----------

LAB bootdevice

IF NOT EXISTS "DEVS:Mountlist"
  REQUESTCHOICE "Error" "Mountlist 'DEVS:Mountlist' doesn't exist!*N*NTry build mountlist" "OK" >NIL: 
  SKIP BACK mainmenu
ENDIF

; Build select boot device menu from device names in mount list
set bootdevicemenu `grep ":$" <DEVS:Mountlist`
echo "$bootdevicemenu" >T:bootdevicemenu
rep T:bootdevicemenu " " "*N"

; Show select boot device menu
set bootdeviceselected "`C:ReqList CLONERT BUTTONS I=T:bootdevicemenu H="Select Boot Device"`"
echo "$bootdeviceselected" NOLINE >T:_bootdeviceselected
echo "q;d" NOLINE >>T:_bootdeviceselected
set lineregexp "`type T:_bootdeviceselected`"
sed "$lineregexp" "T:bootdevicemenu" >"SYS:Prefs/Env-Archive/BootDevice"
delete >NIL: T:bootdevicemenu T:_bootdeviceselected

SKIP BACK mainmenu


; Build mountlist
; ---------------

LAB buildmountlist
echo ""
echo "Building mountlist..."
execute S:BuildMountlist
echo "Done"
echo "Building mount startup..."
execute S:BuildMountStartup DEVS:Mountlist S:Mount-Startup
echo "Done"
echo ""
ask "Press ENTER to continue"
SKIP reboot


; Reboot
; ------
LAB reboot

echo ""
echo "Rebooting in 10 seconds..."

wait 10
reboot


; End
; ---
LAB end